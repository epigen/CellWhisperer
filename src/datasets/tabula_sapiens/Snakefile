from pathlib import Path
import numpy as np
from snakemake.remote.HTTP import RemoteProvider as HTTPRemoteProvider
import subprocess
HTTP = HTTPRemoteProvider()

# Download and preprocess the Tabula Sapiens dataset

PROJECT_DIR = Path(subprocess.check_output("git rev-parse --show-toplevel", shell=True).decode("utf-8").strip())
configfile: PROJECT_DIR / "config.yaml"

H5AD_URL = "https://figshare.com/ndownloader/files/40067134"

rule all:
    input:
        [PROJECT_DIR / config["paths"][dataset_type].format(dataset=dataset_name) for dataset_type in ["read_count_table", "processed_annotations"] for dataset_name in ["tabula_sapiens", "tabula_sapiens_100_cells_per_type", "tabula_sapiens_well_studied_celltypes"]]

rule download_data:
    output:
        temporary("TabulaSapiens.h5ad")
    params:
        url=H5AD_URL
    conda: "../../../envs/unzip.yaml"
    shell:
        """
        wget --user-agent="Mozilla/5.0 (Windows NT 10.0; WOW64) AppleWebKit/537.36 (KHTML, like Gecko) Chrome/51.0.2704.103 Safari/537.36" -O {output[0]}.zip {params.url}
        unzip {output[0]}.zip
        rm {output[0]}.zip
        """

rule process_data:
    """
    """
    input:
        rules.download_data.output[0]
    output:
        read_count_table=PROJECT_DIR / config["paths"]["read_count_table"].format(dataset="tabula_sapiens"),
        structured_annotations_full = PROJECT_DIR / config["paths"]["structured_annotations"].format(dataset="tabula_sapiens"),
    conda: "cellwhisperer"
    script:
        "scripts/process_data.py"

rule subsample_data:
    """
    Subsample 100 cell per cell type
    """
    input:
        read_count_table_full=rules.process_data.output[0]
    output:
        read_count_table_100_cells_per_type=PROJECT_DIR / config["paths"]["read_count_table"].format(dataset="tabula_sapiens_100_cells_per_type"),
        structured_annotations_100percelltype = PROJECT_DIR / config["paths"]["structured_annotations"].format(dataset="tabula_sapiens_100_cells_per_type"),
    conda:
        "cellwhisperer"
    script:
        "scripts/subsample_data.py"

rule subsample_data_well_studied_celltypes:
    """
    """
    input:
        read_count_table_full=rules.process_data.output[0]
    output:
        read_count_table_well_studied_celltypes=PROJECT_DIR / config["paths"]["read_count_table"].format(dataset="tabula_sapiens_well_studied_celltypes"),
        structured_annotations_well_studied_celltypes = PROJECT_DIR / config["paths"]["structured_annotations"].format(dataset="tabula_sapiens_well_studied_celltypes"),
    params:
        well_studied_celltypes=config["top20_lung_liver_blood_celltypes"]  # is all-lowercase
    conda:
        "cellwhisperer"
    script:
        "scripts/subsample_data_well_studied_celltypes.py"

rule processed_annotations:
    """
    we generate processed annotations right away (as a shortcut to circumvent LLM-based processing)
    """
    input: PROJECT_DIR / config["paths"]["read_count_table"]
    output: PROJECT_DIR / config["paths"]["processed_annotations"]
    conda:
        "cellwhisperer"
    script:
        "scripts/processed_annotations.py"
