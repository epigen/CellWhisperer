"""
This root Snakefile downloads the models and all evaluation datasets and then runs all analyses leading to the final figure plots.
"""
include: "shared/config.smk"


module datasets:
    snakefile: "datasets/Snakefile"
    config: config

module figures:
    snakefile: "figures/Snakefile"
    config: config

use rule * from datasets as datasets_*
use rule * from figures as figures_*

GSVA_COMPUTED_DATASETS = ["human_disease", "archs4_geo", "tabula_sapiens", "cellxgene_census"]
rule download_gsva_results:
    """
    Downloading GSVA results, since they are extremely slow to compute

    If you want to run the whole GSVA pipeline, simply comment out this rule and include gsva:
    `include: "../../shared/rules/gsva.smk"`
    """
    input:
        [HTTP.remote(f"{config['precomputing_base_url']}/datasets/{dataset}/gsva.parquet", keep_local=False)[0] for dataset in GSVA_COMPUTED_DATASETS]
    output:
        gsva_results=expand(PROJECT_DIR / config["paths"]["gsva"]["result"], dataset=GSVA_COMPUTED_DATASETS),
    run:
        import shutil
        for fin, fout in zip(input, output):
            shutil.copy(fin, fout)

# include: "shared/rules/gsva.smk"  # Alternatively compute them instead of downloading


LLAVA_BASE_MODEL = config["model_name_path_map"]["llava_base_llm"]
GPU_TYPE = "a100-sxm4-80gb"

include: "shared/rules/genesets.smk"
include: "cellxgene_preprocessing/rules/prepare_cellxgene_dataset.smk"
include: "shared/rules/download_models.smk"

rule figures:
    input:
        rules.figures_all.input,



rule models:
    input:
        expand(rules.download_cellwhisperer_embedding_model.output, cw_model=CW_CLIP_MODELS),  # Download included through
        expand(rules.download_cellwhisperer_llms.output, zip,
               base_model=[config["model_name_path_map"]["llava_base_llm"]] * 3,
               model=[config["model_name_path_map"]["cellwhisperer"], config["model_name_path_map"]["cellwhisperer"], "uce"],
               llava_dataset=["_default", "_celltype", "_top50genescelltype"]),
        rules.download_mixtral.output,
        rules.download_geneformer.output,
        rules.download_llama33.output,
        rules.download_mistral.output

rule all:
    input:
        rules.datasets_all.input,
        rules.figures.input,
        rules.models.input,
    default_target: True
